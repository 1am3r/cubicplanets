# - Try to find OGREOGGSOUND
# Once done, this will define
#
#  OGREOGGSOUND_FOUND - system has OGREOGGSOUND
#  OGREOGGSOUND_INCLUDE_DIRS - the OGREOGGSOUND include directories 
#  OGREOGGSOUND_LIBRARIES - link these to use OGREOGGSOUND
#  OGREOGGSOUND_BINARY_REL / OGREOGGSOUND_BINARY_DBG - DLL names (windows only)

include(FindPkgMacros)
findpkg_begin(OGREOGGSOUND)

# Get path, convert backslashes as ${ENV_${var}}
getenv_path(OGREOGGSOUND_HOME)

# construct search paths
set(OGREOGGSOUND_PREFIX_PATH ${OGREOGGSOUND_HOME} ${ENV_OGREOGGSOUND_HOME})
create_search_paths(OGREOGGSOUND)
# redo search if prefix path changed
clear_if_changed(OGREOGGSOUND_PREFIX_PATH
  OGREOGGSOUND_LIBRARY_FWK
  OGREOGGSOUND_LIBRARY_REL
  OGREOGGSOUND_LIBRARY_DBG
  OGREOGGSOUND_INCLUDE_DIR
)

set(OGREOGGSOUND_LIBRARY_NAMES OgreOggSound)
get_debug_names(OGREOGGSOUND_LIBRARY_NAMES)

use_pkgconfig(OGREOGGSOUND_PKGC OGREOGGSOUND)

findpkg_framework(OGREOGGSOUND)
if (OGREOGGSOUND_HOME)
  set(OGREOGGSOUND_INC_SEARCH_PATH ${OGREOGGSOUND_INC_SEARCH_PATH} ${OGREOGGSOUND_HOME}/include)
endif()

find_path(OGREOGGSOUND_INCLUDE_DIR NAMES OgreOggSound.h HINTS ${OGREOGGSOUND_INC_SEARCH_PATH} ${OGREOGGSOUND_PKGC_INCLUDE_DIRS} PATH_SUFFIXES ogreoggsound)
find_library(OGREOGGSOUND_LIBRARY_REL NAMES ${OGREOGGSOUND_LIBRARY_NAMES} HINTS ${OGREOGGSOUND_LIB_SEARCH_PATH} ${OGREOGGSOUND_PKGC_LIBRARY_DIRS} PATH_SUFFIXES "" release relwithdebinfo minsizerel)
find_library(OGREOGGSOUND_LIBRARY_DBG NAMES ${OGREOGGSOUND_LIBRARY_NAMES_DBG} HINTS ${OGREOGGSOUND_LIB_SEARCH_PATH} ${OGREOGGSOUND_PKGC_LIBRARY_DIRS} PATH_SUFFIXES "" debug)
make_library_set(OGREOGGSOUND_LIBRARY)

if (WIN32)
	set(OGREOGGSOUND_BIN_SEARCH_PATH ${OGREOGGSOUND_HOME}/bin ${ENV_OGREOGGSOUND_HOME}/bin)
	find_file(OGREOGGSOUND_BINARY_REL NAMES "OgreOggSound.dll" HINTS ${OGREOGGSOUND_BIN_SEARCH_PATH}
	  PATH_SUFFIXES "" release relwithdebinfo minsizerel)
	find_file(OGREOGGSOUND_BINARY_DBG NAMES "OgreOggSound_d.dll" HINTS ${OGREOGGSOUND_BIN_SEARCH_PATH}
	  PATH_SUFFIXES "" debug )
endif()
mark_as_advanced(OGREOGGSOUND_BINARY_REL OGREOGGSOUND_BINARY_DBG)


findpkg_finish(OGREOGGSOUND)

# add parent of OGREOGGSOUND folder to support OGREOGGSOUND/OGREOGGSOUND.h
add_parent_dir(OGREOGGSOUND_INCLUDE_DIRS OGREOGGSOUND_INCLUDE_DIR)

